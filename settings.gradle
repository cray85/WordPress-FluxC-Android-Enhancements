pluginManagement {
    gradle.ext.kotlinVersion = '1.6.10'
    gradle.ext.agpVersion = '7.1.1'

    plugins {
        id "org.jetbrains.kotlin.android" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.android.extensions" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.jvm" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.kapt" version gradle.ext.kotlinVersion
        id "com.android.application" version gradle.ext.agpVersion
        id "com.android.library" version gradle.ext.agpVersion
        id "com.automattic.android.publish-to-s3" version "0.7.0"
    }
    repositories {
        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android' 
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
        gradlePluginPortal()
        google()
    }
    resolutionStrategy {
        eachPlugin {
            // TODO: Remove this as soon as fetchstyle starts supporting Plugin Marker Artifacts
            if (requested.id.id == "com.automattic.android.fetchstyle") {
                useModule("com.automattic.android:fetchstyle:1.1")
            }
            // TODO: Remove this as soon as configure starts supporting Plugin Marker Artifacts
            if (requested.id.id == "com.automattic.android.configure") {
                useModule("com.automattic.android:configure:0.6.1")
            }
        }
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.9'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.6.5'
}

def catalogVersion = "trunk-2fc538de6c368b25e5aa7b46c0d7c2e24fa499ee"
dependencyResolutionManagement {
    repositories {
        exclusiveContent {
            forRepository {
                maven {
                    url = uri("https://a8c-libs.s3.amazonaws.com/android")
                }
            }
            filter {
                includeModule("com.automattic", "dependency-catalog")
            }
        }
    }
    versionCatalogs {
        sharedLibs {
            from("com.automattic:dependency-catalog:$catalogVersion")
        }
    }
}

gradleEnterprise {
    server = "https://gradle.a8c.com"
    allowUntrustedServer = false
    buildScan {
        publishAlways()
        capture {
            taskInputFiles = true
        }
        uploadInBackground = System.getenv("CI") == null
    }
}

if (System.getenv().containsKey("CI")) {
    buildCache {
        remote(HttpBuildCache) {
            url = "http://10.0.2.215:5071/cache/"
            allowUntrustedServer = true
            allowInsecureProtocol = true
            push = true
        }
    }
}

rootProject.name = 'FluxC'

include ':fluxc',
        ':fluxc-processor',
        ':fluxc-annotations',
        ':plugins:woocommerce',
        ':example',
        ':tests:api'
