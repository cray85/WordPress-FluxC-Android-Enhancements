plugins {
    // Those declarations are just a workaround for a false-positive Kotlin Gradle Plugin warning
    // https://youtrack.jetbrains.com/issue/KT-46200
    id "com.android.application" apply false
    id "org.jetbrains.kotlin.android" apply false
    id "org.jetbrains.kotlin.android.extensions" apply false
    id "org.jetbrains.kotlin.jvm" apply false
    id "org.jetbrains.kotlin.kapt" apply false

    id "com.automattic.android.fetchstyle"
    id "com.automattic.android.configure"
}

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android"
            content {
                includeGroup "org.wordpress"
                includeGroup "org.wordpress.fluxc"
                includeGroup "org.wordpress.wellsql"
            }
        }
        google()
        mavenCentral()
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android/jcenter-mirror"
            content {
                includeVersion "com.android.volley", "volley", "1.1.1"
                includeVersion "com.facebook.flipper", "flipper", "0.51.0"
                includeVersion "com.facebook.flipper", "flipper-network-plugin", "0.51.0"
            }
        }
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.31.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

ext {
    daggerVersion = '2.29.1'
    wellSqlVersion = '21-4d9ded3274a8e1c6c236156cde1759047135e0bf'
    supportLibraryVersion = '27.1.1'
    arch_paging_version = '1.0.1'
    arch_lifecycle_version = '1.1.1'
    arch_core_version = '2.0.1'
    appcompat_version = '1.0.2'
    mockitoVersion = '3.3.3'
    assertJVersion = '3.19.0'
    kotlinCoroutinesVersion = '1.3.9'
    roomVersion = "2.4.2"
    wordPressUtilsVersion = "develop-eebc5d8e91a1d90190919f900f924b39c861a528"

    fluxcAnnotationsProjectDependency = project.hasProperty("fluxcAnnotationsVersion") ? "org.wordpress.fluxc:fluxc-annotations:${project.getProperty("fluxcAnnotationsVersion")}" : project(":fluxc-annotations")
    fluxcProcessorProjectDependency = project.hasProperty("fluxcProcessorVersion") ? "org.wordpress.fluxc:fluxc-processor:${project.getProperty("fluxcProcessorVersion")}" : project(":fluxc-processor")
    fluxcProjectDependency = project.hasProperty("fluxcVersion") ? "org.wordpress:fluxc:${project.getProperty("fluxcVersion")}" : project(":fluxc")
}
